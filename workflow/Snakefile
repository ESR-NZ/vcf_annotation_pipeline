"""
Author: Miles Benton, Joep de Ligt and Leah Kemp
Affiliation: ESR
Aim: A simple Snakemake workflow to annotate variant call format (VCF) files using GATK4, SnpSift, VEP and genmod. Designed to be used after human_genomics_pipeline.
Date created: 2020-03-06
Modified: 2020-08-28
"""

##### Set up #####

# Define samples from vcf dir using wildcards
if config['DATA'] == "Single" or config['DATA'] == 'single':
    SAMPLES, = glob_wildcards("../../vcf/{sample}_raw_snps_indels.vcf")

if config['DATA'] == "Cohort" or config['DATA'] == 'cohort':
    SAMPLES, = glob_wildcards("../../vcf/{sample}_raw_snps_indels_g.vcf")

##### Target rules #####
if config['DATA'] == "Single" or config['DATA'] == 'single':
    rule all:
           input:
               expand("../results/filtered/{sample}_filtered.vcf", sample = SAMPLES),
               expand("../results/annotated/{sample}_filtered_annotated.vcf", sample = SAMPLES),
               expand("../results/readyforscout/{sample}_filtered_annotated_readyforscout.vcf.gz", sample = SAMPLES)

if config['DATA'] == "Cohort" or config['DATA'] == 'cohort':
    rule all:
           input:
               expand("../results/filtered/{sample}_filtered.vcf", sample = SAMPLES),
               expand("../results/annotated/{sample}_filtered_annotated.vcf", sample = SAMPLES),
               expand("../results/readyforscout/{sample}_filtered_annotated_readyforscout.vcf", sample = SAMPLES)

##### Set up report #####

report: "report/workflow.rst"

##### Load rules #####

localrules: gunzip, bgzip, tabix, pbrun_cnnscorevariants, pbrun_vqsr_indel, pbrun_vqsr_snp

# Variant filtering
if (config['DATA'] == "Single" or config['DATA'] == 'single') and (config['GPU_ACCELERATED'] == "No" or config['GPU_ACCELERATED'] == "no"):
    include: "rules/gatk_CNNScoreVariants.smk"
    include: "rules/gatk_FilterVariantTranches.smk"
        
if (config['DATA'] == "Single" or config['DATA'] == 'single') and (config['GPU_ACCELERATED'] == "Yes" or config['GPU_ACCELERATED'] == "yes"):
    include: "rules/pbrun_cnnscorevariants.smk"
    include: "rules/gatk_FilterVariantTranches.smk"

if (config['DATA'] == "Cohort" or config['DATA'] == 'cohort') and (config['GPU_ACCELERATED'] == "No" or config['GPU_ACCELERATED'] == "no"):
    include: "rules/gatk_VariantRecalibrator_indel.smk"
    include: "rules/gatk_VariantRecalibrator_snp.smk"
    include: "rules/gatk_ApplyVQSR_indel.smk"
    include: "rules/gatk_ApplyVQSR_snp.smk"

if (config['DATA'] == "Cohort" or config['DATA'] == 'cohort') and (config['GPU_ACCELERATED'] == "Yes" or config['GPU_ACCELERATED'] == "yes"):
    include: "rules/pbrun_vqsr_indel.smk"
    include: "rules/pbrun_vqsr_snp.smk"

# Annotation
include: "rules/SnpSift_dbnsfp.smk"
include: "rules/bgzip.smk"
include: "rules/tabix.smk"
include: "rules/vep.smk"
include: "rules/genmod_annotate_CADD.smk"
include: "rules/SnpSift_annotate_dbSNP.smk"

if config['DATA'] == "Cohort" or config['DATA'] == 'cohort':
    include: "rules/gatk_CalculateGenotypePosteriors.smk"
    include: "rules/gatk_VariantAnnotator_PossibleDeNovo.smk"
    include: "rules/genmod_models.smk"

# Filtering to prepare for scout
include: "rules/bcftools_view_multiallelicsites.smk"

if config['DATA'] == "Single" or config['DATA'] == 'single':
    include: "rules/genmod_score.smk"

if config['DATA'] == "Cohort" or config['DATA'] == 'cohort':
    include: "rules/gunzip.smk"
    include: "rules/SnpSift_filter_proband.smk"
    include: "rules/genmod_score.smk"
